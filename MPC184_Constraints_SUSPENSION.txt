FINISH
/CLEAR, START, NEW  ! Clear previous data and start a new session

! --------------------------------------------
! >>>>>> INPUT PARAMETERS <<<<<
! --------------------------------------------

!element thin tube
R_in_thin = 15.05	!inner radius
R_out_thin = 17.45	!outer radius

!element thick tube
R_in_thick = 20.45	!inner radius
R_out_thick = 24.13	!outer radius

! >>>>> MATERIAL PROPERTIES <<<<< TO BE MODIFIED
E_Young = 210000  
ni = 0.29	    
Density=7.85e-6		

! >>>>> other parameters <<<<<
E_length_straight = 10	! element length (straight part)
E_length_arc = 1
Eps = 1E-4	! selection tolerance

! --------------------------------------------
! >>>>> PREPROCESSOR <<<<<
! --------------------------------------------

/PREP7 

ET, 1, BEAM189  
! Material properties
MP, EX, 1, E_Young  
MP, PRXY, 1, ni     
MP, DENS, 1, density  

! Data thick cross section (Cylindrical Tube)
SECTYPE, 1, BEAM, CTUBE, thicktube
SECDATA, R_in_thick, R_out_thick  

! Data thin cross section (Cylindrical Tube)
SECTYPE, 2, BEAM, CTUBE, thintube
SECDATA, R_in_thin, R_out_thin  ! Parametri della sezione del tirante (tubo cavo)

! >>>>> GEOMETRY DEFINITION <<<<<

! Keypoints and lines creation (symmetric part, left side)
K,1,-20.98,113.55,0
K,2,-6.22,33.68,140.67
K,3,62.97,44.17,139.36 ! Centro arco
K,4,67.84,9.53,200
K,5,565.45,79.47,200
K,6,555.71,148.79,200 ! Centro arco
K,7,611.61,106.66,200
K,8,821.60,385.33,200
K,9,877.51,343.2,200 ! Centro arco
K,10,855.88,409.78,200 
K,11,1226.2,530.11,200

! Keypoints and lines creation (symmetric part, right side)
K,12,-6.22,33.68,-140.67  
K,13,62.97,44.17,-139.36 ! Arc center
K,14,67.84,9.53,-200
K,15,565.45,79.47,-200 
K,16,555.71,148.79,-200 ! Arc center
K,17,611.61,106.66,-200
K,18,821.60,385.33,-200 
K,19,877.51,343.2,-200 ! Arc center
K,20,855.88,409.78,-200 
K,21,1226.2,530.11,-200

!Transverse sections
K,22,1048.21,395.72,0 ! Arc center
K,23,1105.73,490.96,200
K,24,1105.73,490.96,-200
K,25,672.18,187.04,200
K,26,672.18,187.04,-200
K,27,300,42.16,200
K,28,300,495.44,0 ! Arc center
K,29,300,42.16,-200

!central section (steering wheel pivot)
K,30,300,0,0
K,31,58.25,0,0
K,32,58.25,70,0 ! Arc center
K,33,-10.58,57.28,0
K,34,-113.26,612.95,0
K,35,-54.65,751.03,0
K,36,-171.87,474.88,0

! Lines creation

! symmetric part, left side
L,1,2
LARC,2,4,3,70
L,4,27
L,27,5
LARC,5,7,6,70
L,7,25
L,25,8
LARC,8,10,9,70
L,10,23
L,23,11

! symmetric part, right side
L,1,12
LARC,12,14,13,70
L,14,29
L,29,15
LARC,15,17,16,70
L,17,26
L,26,18
LARC,18,20,19,70
L,20,24
L,24,21

! Transverse sections
LARC,11,21,22,300
L,23,24
L,25,26
LARC,27,30,28,500
LARC,30,29,28,500

! Central section (steering wheel pivot)
L,30,31
LARC,31,33,32,70
L,33,1
L,1,34
L,35,34
L,34,36

! Element definition

! Left side straight lines
LSEL,S,LINE,,1
LSEL,A,LINE,,3
LSEL,A,LINE,,4
LSEL,A,LINE,,6
LSEL,A,LINE,,7
LSEL,A,LINE,,9
LSEL,A,LINE,,10
! Right side straight lines
LSEL,A,LINE,,11
LSEL,A,LINE,,13
LSEL,A,LINE,,14
LSEL,A,LINE,,16
LSEL,A,LINE,,17
LSEL,A,LINE,,19
LSEL,A,LINE,,20
! Transverse straight lines
LSEL,A,LINE,,22
LSEL,A,LINE,,23


LESIZE, ALL,E_length_straight
LATT,1,,1,,,,2
LSEL,ALL,ALL

! Left side arc lines
LSEL,S,LINE,,2
LSEL,A,LINE,,5
LSEL,A,LINE,,8
! Right side arc lines
LSEL,A,LINE,,12
LSEL,A,LINE,,15
LSEL,A,LINE,,18
! Transverse arc lines
LSEL,A,LINE,,21
LSEL,A,LINE,,24
LSEL,A,LINE,,25

LESIZE, ALL,E_length_arc
LATT,1,,1,,,,2
LSEL,ALL,ALL
! Central section straight lines (thick tube)
LSEL,S,LINE,,26
LSEL,A,LINE,,28
LSEL,A,LINE,,29
LSEL,A,LINE,,30
LSEL,A,LINE,,31
LESIZE, ALL,E_length_straight
LATT,1,,1,,,,1
LSEL,ALL,ALL
! Central section arc lines (thick tube)
LSEL,S,LINE,,27
LESIZE, ALL,E_length_arc
LATT,1,,1,,,,1
LSEL,ALL,ALL

! ORIENTATION OF SECTION FOR L3, L4
LSEL, S, LINE,,3
LSEL, A, LINE,,4
LATT,1,,1,,6,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L13, L14
LSEL, S, LINE,,13
LSEL, A, LINE,,14
LATT,1,,1,,16,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L9, L10
LSEL, S, LINE,,9
LSEL, A, LINE,,10
LATT,1,,1,,9,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L19, L20
LSEL, S, LINE,,19
LSEL, A, LINE,,20
LATT,1,,1,,19,,2
LSEL, ALL, ALL

! MESHING
LMESH,ALL
NUMMRG,NODE,0.01

! MPC184 ELEMENTS CREATION
! Define bearing position of the swingarm LEFT SIDE (Master nodes)
*GET, max_node_L, NODE, 0, NUM, MAXD
N, max_node_L+1, 664.58, 86.38, 205.0

ET,2,184
KEYOPT,2,1,1
TYPE,2
! Select nodes for the left side (Slave nodes)
LSEL, S, LINE,,5
LSEL, A, LINE,,6
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_L+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Define bearing position of the swingarm RIGHT SIDE (Master nodes)
*GET, max_node_R, NODE, 0, NUM, MAXD
N, max_node_R+1, 664.58, 86.38, -205.0
! Select nodes for the right side (Slave nodes)
LSEL, S, LINE,,15
LSEL, A, LINE,,16
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_R+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Create swingarm pivot with MPC184
*GET, max_node_C, NODE, 0, NUM, MAX 
N, max_node_C+1, 664.58, 86.38, 0.0 ! center of the swingarm pivot
E, max_node_C+1, max_node_L+1
E, max_node_C+1, max_node_R+1

ALLSEL, ALL

! Define the center of the rear suspension pivot LEFT SIDE (Master nodes)
*GET, max_node_L_SA, NODE, 0, NUM, MAXD
N, max_node_L_SA+1, 1101.3, 433.5, 200
!N,3866,1101.3,433.5,-200
! MPC184 element
ET,2,184
KEYOPT,2,1,1
TYPE,2
! Define a local RF. positioned at K23 and oriented as the beam
LOCAL,11,0         
CSYS,0
LOCAL,11,0, 1105.7,491.0,200,18.0
CSYS,11
! Select the nodes that lies in the interval +35, -35 mm from the RF
NSEL, S, LOC, Z, 0.0 - Eps, 0.0 + Eps
NSEL, R, LOC, Y, 0.0 - 1, 0.0 + 1
NSEL, R, LOC, X, 0.0 - 35, 0.0 + 35
CSYS,0
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_L_SA+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Define the center of the rear suspension pivot RIGHT SIDE (Master nodes)
*GET, max_node_R_SA, NODE, 0, NUM, MAXD
N,max_node_R_SA+1,1101.3,433.5,-200
! Define a local RF. positioned at K23 and oriented as the beam
LOCAL,12,0         
CSYS,0
LOCAL,12,0, 1105.7,491.0,-200,18.0
CSYS,12
! Select the nodes that lies in the interval +35, -35 mm from the RF
NSEL, S, LOC, Z, 0.0 - Eps, 0.0 + Eps
NSEL, R, LOC, Y, 0.0 - 1, 0.0 + 1
NSEL, R, LOC, X, 0.0 - 35, 0.0 + 35
CSYS,0
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_R_SA+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! MPC184 elements for the steering head (KP 35 and 36)
KSEL, S, KP, , 35
KSEL, A, KP, , 36
NSLK, S
*GET, node_UP, NODE, 0, NUM, MIN
*GET, node_DOWN, NODE, 0, NUM, MAX
E, node_UP, node_DOWN
ALLSEL, ALL

! Define the front wheel pivot (Master nodes)
*GET, max_node_F, NODE, 0, NUM, MAXD
N, max_node_F+1, -373.4, 0, 0
NSEL, S, NODE, , max_node_F+1
NSEL, A, NODE, , node_DOWN
E, max_node_F+1, node_DOWN

CSYS,0
ALLSEL, ALL

! Define the front wheel contact point
*GET, max_node_FC, NODE, 0, NUM, MAXD
N, max_node_FC+1, -373.4, -240.0, 0

! MPC184 elements for the front wheel contact point
NSEL, S, NODE, , max_node_FC+1
NSEL, A, NODE, , max_node_F+1
E, max_node_FC+1, max_node_F+1

ALLSEL, ALL

! Define the handlebar position
*GET, max_node_H, NODE, 0, NUM, MAXD
N, max_node_H+1, 3.960, 889.1, 0
! MPC184 elements for the handlebar
NSEL, S, NODE, , max_node_H+1
NSEL, A, NODE, , node_UP
E, max_node_H+1, node_UP

ALLSEL, ALL

! Create swingarm - wheel connection
N, 10000, 664.58, 86.38, 205.0 ! left swingarm
N, 10001, 664.58, 86.38, -205.0! right swingarm
N, 10002, 936.56, 86.38, 200 ! left swingarm wheel connection
N, 20002, 936.56, 86.38, 200 ! left swingarm wheel connection 2
N, 30002, 936.56, 86.38, 200 ! left swingarm wheel connection 3
N, 10003, 936.56, 86.38, -200 ! right swingarm wheel connection
N, 20003, 936.56, 86.38, -200 ! right swingarm wheel connection 2
N, 30003, 936.56, 86.38, -200 ! right swingarm wheel connection 3
N, 10004, 1101.3, 433.5, 200 ! left shock absorber mounting point
N, 10005, 1101.3, 433.5, -200 ! right shock absorber mounting point

! Local reference frames
LOCAL, 10000
LOCAL, 10001
LOCAL, 10002
LOCAL, 20002
LOCAL, 10003
LOCAL, 20003
LOCAL, 10004
LOCAL, 10005

! Internal pins
ET, 3, MPC184 ! revolute joint element
KEYOPT,3,1,6
KEYOPT,3,4,1
KEYOPT,3,2,1
SECTYPE,3,JOINT,REVO,REVOLUTE   
SECJOINT,,   10000,   10001 
SECJOINT,FRIC,0,0,0 
SECDATA,,,, ,,  
TYPE,   3   
MAT,       1
REAL,   
ESYS,   10000   
SECNUM,   3 

! Create revolute joint for the swingarm left side
SECJOINT, LSYS, 10000
E, 10000, max_node_L+1

! Create revolute joint for the swingarm right side
SECJOINT, LSYS, 10001
E, 10001, max_node_R+1

! Create revolute joint for the swingarm wheel left side
SECJOINT, LSYS, 10002
E, 10002, 20002
! Create revolute joint for the swingarm wheel right side
SECJOINT, LSYS, 10003
E, 10003, 20003

! Create revolute joint for the shock absorber left side
SECJOINT, LSYS, 10004
E, 10004, max_node_L_SA+1
! Create revolute joint for the shock absorber right side
SECJOINT, LSYS, 10005
E, 10005, max_node_R_SA+1


! Connect elements
ET, 4, COMBIN14 ! COMBIN14 element for the shock absorbers
KEYOPT,4,1,0
KEYOPT,4,2,0
KEYOPT,4,3,0
TYPE, 4

R,1,16, , , , , ,
RMORE, ,
! Connect shock absorbers to wheel's revolute joints
E, max_node_L_SA+1, 10002
E, max_node_R_SA+1, 10003

TYPE, 2
! Connect swingarm to wheel's revolute joints
E, 10000, 10002
E, 10001, 10003


ALLSEL, ALL

/ESHAPE,1
/VIEW, 1, 1, 1, 1
EPLOT
FINISH  


! --------------------------------------------
! >>>>> SOLUTION <<<<<
! --------------------------------------------

/SOLUTION
NSUBST,10,100,2
NROPT, FULL, , OFF
NLGEOM, ON
AUTOTS, ON
ANTYPE, STATIC, NEW   !static solution

! >>>>> CONSTRAINT DEFINITION <<<<<<

! Constrain the front wheel's contact point (flip-over limit)
NSEL, S, NODE, , max_node_FC+1
 D, ALL, UX, 0
 D, ALL, UY, 0
 D, ALL, UZ, 0
 D, ALL, ROTX,0
 D, ALL, ROTY,0
 ! D, ALL, ROTZ,0 free rotation around Z axis
ALLSEL, ALL

! Constrain the rear wheel
NSEL, S, NODE, , 20002
 D, ALL, UY, 0
 D, ALL, UZ, 0
 D, ALL, ROTX,0
 D, ALL, ROTY,0
ALLSEL, ALL
NSEL, S, NODE, , 20003
 D, ALL, UY, 0
 D, ALL, UZ, 0
 D, ALL, ROTX,0
 D, ALL, ROTY,0
ALLSEL, ALL


! >>>>> LOAD DEFINITION <<<<<

! Geometric data
L1 = 0.600 ! (m) 
p = 1.4 ! (m)

! External forces data
g = 9.81 ! (m/s^2) 
m_r = 75 ! (kg) mass of rider and passenger
m_p = 50 ! (kg) mass of passenger
m_kerb = 120 ! (kg) mass of the vehicle
M = m_kerb+m_r+m_p ! (kg) Total mass of the vehicle and passengers
W_r = m_r*g ! (N) Weight of rider
W_p = m_p*g ! (N) Weight of passenger
Wtot = M*g ! (N) Total weight
W1 = W_r*0.5 ! (N) Weight on the cradle (rider)
W2 = W_r*0.5 ! (N) Weight on the front seat (rider)
W3 = W_p ! (N) Weight on the back seat (passenger)
Wbatt = 21*g ! Battery weight (N)
Weng = 30*g ! Engine weight (N)

m_chassis = 20 ! Mass of the chassis (kg)
m_res = m_kerb-21-30 ! Residual mass of the vehicle
a_y_eq = g*(m_res/m_chassis) ! = 3.4*g Equivalent acceleration in the y direction (m/s^2)

! LOADS

! Cradle
W1_half = W1/2
LSEL, S, LINE,,3
*GET, L3_length, LINE, 3, LENGTH
w1_half_press = W1_half/(L3_length) ! Pressure on the lines (N/mm)
ESLL, S
SFBEAM, ALL, 1, PRES, w1_half_press
LSEL, S, LINE,,13
ESLL, S
SFBEAM, ALL, 1, PRES, w1_half_press

ALLSEL,ALL

! Front seat
W2_half = W2/2
*GET, L9_length, LINE, 9, LENGTH
LSEL, S, LINE,,9
w2_half_press = W2_half/L9_length ! Pressure on the lines (N/mm^2 or MPa)
ESLL, S
SFBEAM, ALL, 1, PRES, -w2_half_press
LSEL, S, LINE,,19
ESLL, S
SFBEAM, ALL, 1, PRES, -w2_half_press

ALLSEL,ALL

! Back seat
W3_half = W3/2
*GET, L10_length, LINE, 10, LENGTH
LSEL, S, LINE,,10
w3_half_press = W3_half/(L10_length+L9_length) ! Pressure on the lines (N/mm^2 or MPa)
ESLL, S
SFBEAM, ALL, 1, PRES, -w3_half_press
LSEL, S, LINE,,20
ESLL, S
SFBEAM, ALL, 1, PRES, -w3_half_press

ALLSEL, ALL


! Battery
Wbatt_half = Wbatt/2
LSEL, S, LINE,,4 ! Left side line 4
*GET, L4_length, LINE, 4, LENGTH
w_batt_half_press = Wbatt_half/L4_length ! Pressure on the line (N/mm)
ESLL, S
SFBEAM, ALL, 1, PRES, w_batt_half_press
LSEL, S, LINE,,14  ! Right side line 14
ESLL, S
SFBEAM, ALL, 1, PRES, w_batt_half_press
ALLSEL,ALL

! Engine
Weng_half = Weng/2
NSEL, S, NODE, , max_node_C+1 ! Center of the swingarm pivot
F, max_node_C+1, FY, -W_engine
ALLSEL, ALL

ACEL,,a_y_eq	!m/s2

outres,all,all
Eqslv,pcg
Solve

FINISH
/REPLOT

/POST1
SET,1
PLDISP,2