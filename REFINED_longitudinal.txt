FINISH
/CLEAR, START, NEW  ! Clear previous data and start a new session

! Import the CDB mesh

! Set the specific model!
/INPUT,'canotto_reinforced','cdb'   ! Import the meshed section

! Parameters definition

!element thin tube
R_in_thin = 15.05	!inner radius
R_out_thin = 17.45	!outer radius

!element thick tube
R_in_thick = 20.45	!inner radius
R_out_thick = 24.13	!outer radius

! >>>>> MATERIAL PROPERTIES <<<<< TO BE MODIFIED
E_Young = 210000  
ni = 0.29	    
Density=7.85e-6		

! >>>>> other parameters <<<<<
E_length_straight = 10	! element length (straight part)
E_length_arc = 1
Eps = 1E-4	! selection tolerance



/PREP7  ! ModalitÃ  di pre-processing

ET, 2, BEAM189  
! Material properties
MP, EX, 1, E_Young  
MP, PRXY, 1, ni     
MP, DENS, 1, density

! assign material properties to the mesh elements
ALLSEL, ALL
EMODIF, ALL, MAT, 1

! Data thick cross section (Cylindrical Tube)
SECTYPE, 1, BEAM, CTUBE, thicktube
SECDATA, R_in_thick, R_out_thick  

! Data thin cross section (Cylindrical Tube)
SECTYPE, 2, BEAM, CTUBE, thintube
SECDATA, R_in_thin, R_out_thin  ! Parametri della sezione del tirante (tubo cavo)

! >>>>> GEOMETRY DEFINITION <<<<<

! Keypoints and lines creation (symmetric part, left side)
K,1,-20.98,113.55,0
K,2,-6.22,33.68,140.67
K,3,62.97,44.17,139.36 ! Centro arco
K,4,67.84,9.53,200
K,5,565.45,79.47,200
K,6,555.71,148.79,200 ! Centro arco
K,7,611.61,106.66,200
K,8,821.60,385.33,200
K,9,877.51,343.2,200 ! Centro arco
K,10,855.88,409.78,200 
K,11,1226.2,530.11,200

! Keypoints and lines creation (symmetric part, right side)
K,12,-6.22,33.68,-140.67  
K,13,62.97,44.17,-139.36 ! Arc center
K,14,67.84,9.53,-200
K,15,565.45,79.47,-200 
K,16,555.71,148.79,-200 ! Arc center
K,17,611.61,106.66,-200
K,18,821.60,385.33,-200 
K,19,877.51,343.2,-200 ! Arc center
K,20,855.88,409.78,-200 
K,21,1226.2,530.11,-200

!Transverse sections
K,22,1048.21,395.72,0 ! Arc center
K,23,1105.73,490.96,200
K,24,1105.73,490.96,-200
K,25,672.18,187.04,200
K,26,672.18,187.04,-200
K,27,300,42.16,200
K,28,300,495.44,0 ! Arc center
K,29,300,42.16,-200

!central section (steering wheel pivot)
K,30,300,0,0
K,31,58.25,0,0
K,32,58.25,70,0 ! Arc center
K,33,-10.58,57.28,0
K,34,-86.00,465.45,0 ! Point of connnection with the imported mesh

! Lines creation

! symmetric part, left side
L,1,2
LARC,2,4,3,70
L,4,27
L,27,5
LARC,5,7,6,70
L,7,25
L,25,8
LARC,8,10,9,70
L,10,23
L,23,11

! symmetric part, right side
L,1,12
LARC,12,14,13,70
L,14,29
L,29,15
LARC,15,17,16,70
L,17,26
L,26,18
LARC,18,20,19,70
L,20,24
L,24,21

! Transverse sections
LARC,11,21,22,300
L,23,24
L,25,26
LARC,27,30,28,500
LARC,30,29,28,500

! Central section (steering wheel pivot)
L,30,31
LARC,31,33,32,70
L,33,1
L,1,34

! Element definition

! Left side straight lines
LSEL,S,LINE,,1
LSEL,A,LINE,,3
LSEL,A,LINE,,4
LSEL,A,LINE,,6
LSEL,A,LINE,,7
LSEL,A,LINE,,9
LSEL,A,LINE,,10
! Right side straight lines
LSEL,A,LINE,,11
LSEL,A,LINE,,13
LSEL,A,LINE,,14
LSEL,A,LINE,,16
LSEL,A,LINE,,17
LSEL,A,LINE,,19
LSEL,A,LINE,,20
! Transverse straight lines
LSEL,A,LINE,,22
LSEL,A,LINE,,23


LESIZE, ALL,E_length_straight
LATT,1,,2,,,,2
LSEL,ALL,ALL

! Left side arc lines
LSEL,S,LINE,,2
LSEL,A,LINE,,5
LSEL,A,LINE,,8
! Right side arc lines
LSEL,A,LINE,,12
LSEL,A,LINE,,15
LSEL,A,LINE,,18
! Transverse arc lines
LSEL,A,LINE,,21
LSEL,A,LINE,,24
LSEL,A,LINE,,25

LESIZE, ALL,E_length_arc
LATT,1,,2,,,,2
LSEL,ALL,ALL
! Central section straight lines (thick tube)
LSEL,S,LINE,,26
LSEL,A,LINE,,28
LSEL,A,LINE,,29
LESIZE, ALL,E_length_straight
LATT,1,,2,,,,1
LSEL,ALL,ALL
! Central section arc lines (thick tube)
LSEL,S,LINE,,27
LESIZE, ALL,E_length_arc
LATT,1,,2,,,,1
LSEL,ALL,ALL

! ORIENTATION OF SECTION FOR L3, L4
LSEL, S, LINE,,3
LSEL, A, LINE,,4
LATT,1,,2,,6,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L13, L14
LSEL, S, LINE,,13
LSEL, A, LINE,,14
LATT,1,,2,,16,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L9, L10
LSEL, S, LINE,,9
LSEL, A, LINE,,10
LATT,1,,2,,9,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L19, L20
LSEL, S, LINE,,19
LSEL, A, LINE,,20
LATT,1,,2,,19,,2
LSEL, ALL, ALL

LMESH,ALL
NUMMRG,NODE,0.01

! MPC184 element
ET,3,184
KEYOPT,3,1,1 ! Rigid beam
KEYOPT,3,2,1 ! Lagrange multiplier method
TYPE,3

! Interface between steering head and the rest of the frame, using MPC184

ESEL, S, TYPE, , 2 !select only the BEAM189 nodes
NSEL, R, LOC, Z, 0.0 - Eps, 0.0 + Eps
NSEL, R, LOC, Y, 465.45 - eps, 465.45 + eps
NSEL, R, LOC, X, -86.00 - eps, -86.00 + eps
*GET, int_node, NODE, 0, NUM, MIN
ALLSEL, ALL


ESEL, S, TYPE, , 1 !select only the SOLID187 nodes

LOCAL,11,0         
CSYS,0
LOCAL,11,0, -86.00,465.45,0,10.469
CSYS,11
NSEL, S, LOC, Z, -30 , 30
NSEL, R, LOC, Y, 0.0 -eps, 0.0 + eps
NSEL, R, LOC, X, -30, 30
CSYS,0
! Connect SOLID187 nodes to the BEAM189 nodes
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes, 1
    *GET, n_node, NODE, 0, NUM, MAX
    *IF, n_node, NE, int_node, THEN
        E, int_node, n_node
    *ENDIF
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Define bearing position of the swingarm LEFT SIDE (Master nodes)
*GET, max_node_L, NODE, 0, NUM, MAXD
N, max_node_L+1, 664.58, 86.38, 205.0

! Select nodes for the left side (Slave nodes)
LSEL, S, LINE,,5
LSEL, A, LINE,,6
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_L+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Define bearing position of the swingarm RIGHT SIDE (Master nodes)
*GET, max_node_R, NODE, 0, NUM, MAXD
N, max_node_R+1, 664.58, 86.38, -205.0
! Select nodes for the right side (Slave nodes)
LSEL, S, LINE,,15
LSEL, A, LINE,,16
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_R+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Create swingarm pivot with MPC184
*GET, max_node_C, NODE, 0, NUM, MAX 
N, max_node_C+1, 664.58, 86.38, 0.0 ! center of the swingarm pivot
E, max_node_C+1, max_node_L+1
E, max_node_C+1, max_node_R+1

! Local RF in LEFT SIDE side of pivot
LOCAL,11,0         
CSYS,0
LOCAL,11,0, 664.58, 86.38, 205.0, 67, 0, 0
NSEL, S, NODE, , max_node_L+1 ! rotate nodal ref frame
CSYS,11
NROTAT, ALL
CSYS,0
ALLSEL, ALL
! Local RF in RIGHT SIDE of pivot
LOCAL,12,0
CSYS,0
LOCAL,12,0, 664.58, 86.38,-205.0, 67, 0, 0
NSEL, S, NODE, , max_node_R+1 ! rotate nodal ref frame
CSYS,12
NROTAT, ALL
CSYS,0
ALLSEL, ALL
! Local RF in CENTRAL position of pivot
LOCAL,13,0
CSYS,0
LOCAL,13,0, 664.58, 86.38, 0.0, 67, 0, 0
NSEL, S, NODE, , max_node_C+1 ! rotate nodal ref frame
CSYS,13
NROTAT, ALL
CSYS,0
ALLSEL, ALL

! Model the steering head pin
*GET, node_DOWN, NODE, 0, NUM, MAXD
N, node_DOWN+1, -171.87,474.88,0 ! node down
*GET, node_UP, NODE, 0, NUM, MAXD
N, node_UP+1, -54.65,751.3,0 ! node down
ALLSEL, ALL

! SELECT BEARING THICKNESS
bearing_thk = 15 ! [mm]
! Lower bearing
LOCAL, 14,1
CSYS,0
LOCAL,14,1, -171.87,474.88,0,-23.0,-90
CSYS,14
NSEL, S, LOC, Z, 0.0 - Eps, bearing_thk + Eps 
NSEL, R, LOC, X, R_in_thick - 0.1, R_in_thick + 0.1
CSYS,0
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes, 1
    *GET, n_node, NODE, 0, NUM, MAX
    *IF, n_node, NE, node_DOWN+1, THEN
        E, node_DOWN+1, n_node
    *ENDIF
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL
CSYS, 0

! Upper bearing
CSYS, 14
NSEL, S, LOC, Z, 300 -bearing_thk - Eps, 300 + Eps
NSEL, R, LOC, X, R_in_thick - 0.1, R_in_thick + 0.1
CSYS, 0
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes, 1
    *GET, n_node, NODE, 0, NUM, MAX
    *IF, n_node, NE, node_UP+1, THEN
        E, node_UP+1, n_node
    *ENDIF
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL
CSYS, 0

! Connect the two bearings to simulate the presence of the steering head pin
E, node_DOWN+1, node_UP+1
! Connect the steering head pin to the point of application of the load
*GET, node_LOAD, NODE, 0, NUM, MAX
N, node_LOAD+1, -373.4, 0.0, 0.0 ! point of application of the load
E, node_LOAD+1, node_DOWN+1
ALLSEL, ALL

/ESHAPE,1
/VIEW, 1, 1, 1, 1
EPLOT
FINISH

! --------------------------------------------
! >>>>> SOLUTION <<<<<
! --------------------------------------------

/SOLUTION
ANTYPE, STATIC, NEW   ! static solution

! Swingarm bearings constraints (KP 35 and 36)
NSEL, S, NODE, , max_node_L+1
NSEL, A, NODE, , max_node_R+1
D, ALL, ALL
ALLSEL, ALL

! Parameters
F = 400 ! N
intx_m = 0.4 ! m
intx_mm = 400 ! mm
Torque_Nmm = F*intx_mm ! Nmm
Torque_Nm = F*intx_m ! Nm
toRad = 3.14159265359/180 ! convert degrees to radians
toDeg = 180/3.14159265359 ! convert radians to degrees

! Apply longitudinal load to steering head axis (local ref frame xx)
NSEL, S, NODE, , node_LOAD+1
F, node_LOAD+1, FX, F ! N
ALLSEL, ALL

OUTRES, ALL, ALL
EQSLV, PCG
SOLVE

FINISH
/REPLOT

! --------------------------------------------
! >>>>> POSTPROCESSING <<<<<
! --------------------------------------------

/POST1
SET,1
PLDISP,2

! Get displacement values (global reference system) of the pivot center
*GET, ux_LOAD, NODE, node_LOAD+1, U, X
!*GET, uy_FRONT, NODE, node_FRONT+1, U, Y
!*GET, uz_FRONT, NODE, node_FRONT+1, U, Z
!utot_FRONT = SQRT(ux_FRONT**2 + uy_FRONT**2 + uz_FRONT**2)
utot_LOAD = ux_LOAD

Klong_N_mm = F/utot_LOAD       ! N/mm
Klong_N_m = (F/utot_LOAD)*1000 ! N/m
Klong_kN_mm = (F / utot_LOAD) / 1000 ! kN/mm