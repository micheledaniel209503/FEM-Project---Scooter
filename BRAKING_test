FINISH
/CLEAR, START, NEW  ! Clear previous data and start a new session

! --------------------------------------------
! >>>>>> INPUT PARAMETERS <<<<<
! --------------------------------------------

!element thin tube
R_in_thin = 15.05	!inner radius
R_out_thin = 17.45	!outer radius

!element thick tube
R_in_thick = 20.45	!inner radius
R_out_thick = 24.13	!outer radius

! >>>>> MATERIAL PROPERTIES <<<<< TO BE MODIFIED
E_Young = 210000  
ni = 0.29	    
Density=7.85e-6		

! >>>>> other parameters <<<<<
E_length_straight = 10	! element length (straight part)
E_length_arc = 1
Eps = 1E-4	! selection tolerance

! --------------------------------------------
! >>>>> PREPROCESSOR <<<<<
! --------------------------------------------

/PREP7 

ET, 1, BEAM189  
! Material properties
MP, EX, 1, E_Young  
MP, PRXY, 1, ni     
MP, DENS, 1, density  

! Data thick cross section (Cylindrical Tube)
SECTYPE, 1, BEAM, CTUBE, thicktube
SECDATA, R_in_thick, R_out_thick  

! Data thin cross section (Cylindrical Tube)
SECTYPE, 2, BEAM, CTUBE, thintube
SECDATA, R_in_thin, R_out_thin  ! Parametri della sezione del tirante (tubo cavo)

! >>>>> GEOMETRY DEFINITION <<<<<

! Keypoints and lines creation (symmetric part, left side)
K,1,-20.98,113.55,0
K,2,-6.22,33.68,140.67
K,3,62.97,44.17,139.36 ! Centro arco
K,4,67.84,9.53,200
K,5,565.45,79.47,200
K,6,555.71,148.79,200 ! Centro arco
K,7,611.61,106.66,200
K,8,821.60,385.33,200
K,9,877.51,343.2,200 ! Centro arco
K,10,855.88,409.78,200 
K,11,1226.2,530.11,200

! Keypoints and lines creation (symmetric part, right side)
K,12,-6.22,33.68,-140.67  
K,13,62.97,44.17,-139.36 ! Arc center
K,14,67.84,9.53,-200
K,15,565.45,79.47,-200 
K,16,555.71,148.79,-200 ! Arc center
K,17,611.61,106.66,-200
K,18,821.60,385.33,-200 
K,19,877.51,343.2,-200 ! Arc center
K,20,855.88,409.78,-200 
K,21,1226.2,530.11,-200

!Transverse sections
K,22,1048.21,395.72,0 ! Arc center
K,23,1105.73,490.96,200
K,24,1105.73,490.96,-200
K,25,672.18,187.04,200
K,26,672.18,187.04,-200
K,27,300,42.16,200
K,28,300,495.44,0 ! Arc center
K,29,300,42.16,-200

!central section (steering wheel pivot)
K,30,300,0,0
K,31,58.25,0,0
K,32,58.25,70,0 ! Arc center
K,33,-10.58,57.28,0
K,34,-113.26,612.95,0
K,35,-54.65,751.03,0
K,36,-171.87,474.88,0

! Lines creation

! symmetric part, left side
L,1,2
LARC,2,4,3,70
L,4,27
L,27,5
LARC,5,7,6,70
L,7,25
L,25,8
LARC,8,10,9,70
L,10,23
L,23,11

! symmetric part, right side
L,1,12
LARC,12,14,13,70
L,14,29
L,29,15
LARC,15,17,16,70
L,17,26
L,26,18
LARC,18,20,19,70
L,20,24
L,24,21

! Transverse sections
LARC,11,21,22,300
L,23,24
L,25,26
LARC,27,30,28,500
LARC,30,29,28,500

! Central section (steering wheel pivot)
L,30,31
LARC,31,33,32,70
L,33,1
L,1,34
L,35,34
L,34,36

! Element definition

! Left side straight lines
LSEL,S,LINE,,1
LSEL,A,LINE,,3
LSEL,A,LINE,,4
LSEL,A,LINE,,6
LSEL,A,LINE,,7
LSEL,A,LINE,,9
LSEL,A,LINE,,10
! Right side straight lines
LSEL,A,LINE,,11
LSEL,A,LINE,,13
LSEL,A,LINE,,14
LSEL,A,LINE,,16
LSEL,A,LINE,,17
LSEL,A,LINE,,19
LSEL,A,LINE,,20
! Transverse straight lines
LSEL,A,LINE,,22
LSEL,A,LINE,,23


LESIZE, ALL,E_length_straight
LATT,1,,1,,,,2
LSEL,ALL,ALL

! Left side arc lines
LSEL,S,LINE,,2
LSEL,A,LINE,,5
LSEL,A,LINE,,8
! Right side arc lines
LSEL,A,LINE,,12
LSEL,A,LINE,,15
LSEL,A,LINE,,18
! Transverse arc lines
LSEL,A,LINE,,21
LSEL,A,LINE,,24
LSEL,A,LINE,,25

LESIZE, ALL,E_length_arc
LATT,1,,1,,,,2
LSEL,ALL,ALL
! Central section straight lines (thick tube)
LSEL,S,LINE,,26
LSEL,A,LINE,,28
LSEL,A,LINE,,29
LSEL,A,LINE,,30
LSEL,A,LINE,,31
LESIZE, ALL,E_length_straight
LATT,1,,1,,,,1
LSEL,ALL,ALL
! Central section arc lines (thick tube)
LSEL,S,LINE,,27
LESIZE, ALL,E_length_arc
LATT,1,,1,,,,1
LSEL,ALL,ALL

! ORIENTATION OF SECTION FOR L3, L4
LSEL, S, LINE,,3
LSEL, A, LINE,,4
LATT,1,,1,,6,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L13, L14
LSEL, S, LINE,,13
LSEL, A, LINE,,14
LATT,1,,1,,16,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L9, L10
LSEL, S, LINE,,9
LSEL, A, LINE,,10
LATT,1,,1,,9,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L19, L20
LSEL, S, LINE,,19
LSEL, A, LINE,,20
LATT,1,,1,,19,,2
LSEL, ALL, ALL

! MESHING
LMESH,ALL
NUMMRG,NODE,0.01

! MPC184 ELEMENTS CREATION
! Define bearing position of the swingarm LEFT SIDE (Master nodes)
*GET, max_node_L, NODE, 0, NUM, MAXD
N, max_node_L+1, 664.58, 86.38, 205.0

ET,2,184
KEYOPT,2,1,1
TYPE,2
! Select nodes for the left side (Slave nodes)
LSEL, S, LINE,,5
LSEL, A, LINE,,6
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_L+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Define bearing position of the swingarm RIGHT SIDE (Master nodes)
*GET, max_node_R, NODE, 0, NUM, MAXD
N, max_node_R+1, 664.58, 86.38, -205.0
! Select nodes for the right side (Slave nodes)
LSEL, S, LINE,,15
LSEL, A, LINE,,16
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_R+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Create swingarm pivot with MPC184
*GET, max_node_C, NODE, 0, NUM, MAX 
N, max_node_C+1, 664.58, 86.38, 0.0 ! center of the swingarm pivot
E, max_node_C+1, max_node_L+1
E, max_node_C+1, max_node_R+1

ALLSEL, ALL

! Define the center of the rear suspension pivot LEFT SIDE (Master nodes)
*GET, max_node_L_SA, NODE, 0, NUM, MAXD
N, max_node_L_SA+1, 1101.3, 433.5, 200
!N,3866,1101.3,433.5,-200
! MPC184 element
ET,2,184
KEYOPT,2,1,1
TYPE,2
! Define a local RF. positioned at K23 and oriented as the beam
LOCAL,11,0         
CSYS,0
LOCAL,11,0, 1105.7,491.0,200,18.0
CSYS,11
! Select the nodes that lies in the interval +35, -35 mm from the RF
NSEL, S, LOC, Z, 0.0 - Eps, 0.0 + Eps
NSEL, R, LOC, Y, 0.0 - 1, 0.0 + 1
NSEL, R, LOC, X, 0.0 - 35, 0.0 + 35
CSYS,0
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_L_SA+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! Define the center of the rear suspension pivot RIGHT SIDE (Master nodes)
*GET, max_node_R_SA, NODE, 0, NUM, MAXD
N,max_node_R_SA+1,1101.3,433.5,-200
! Define a local RF. positioned at K23 and oriented as the beam
LOCAL,12,0         
CSYS,0
LOCAL,12,0, 1105.7,491.0,-200,18.0
CSYS,12
! Select the nodes that lies in the interval +35, -35 mm from the RF
NSEL, S, LOC, Z, 0.0 - Eps, 0.0 + Eps
NSEL, R, LOC, Y, 0.0 - 1, 0.0 + 1
NSEL, R, LOC, X, 0.0 - 35, 0.0 + 35
CSYS,0
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_R_SA+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO
ALLSEL, ALL

! MPC184 elements for the steering head (KP 35 and 36)
KSEL, S, KP, , 35
KSEL, A, KP, , 36
NSLK, S
*GET, node_UP, NODE, 0, NUM, MIN
*GET, node_DOWN, NODE, 0, NUM, MAX
E, node_UP, node_DOWN
ALLSEL, ALL

! Define the front wheel pivot (Master nodes)
*GET, max_node_F, NODE, 0, NUM, MAXD
N, max_node_F+1, -373.4, 0, 0
NSEL, S, NODE, , max_node_F+1
NSEL, A, NODE, , node_DOWN
E, max_node_F+1, node_DOWN

CSYS,0
ALLSEL, ALL

! Define the front wheel contact point
*GET, max_node_FC, NODE, 0, NUM, MAXD
N, max_node_FC+1, -373.4, -240.0, 0

! MPC184 elements for the front wheel contact point
NSEL, S, NODE, , max_node_FC+1
NSEL, A, NODE, , max_node_F+1
E, max_node_FC+1, max_node_F+1

ALLSEL, ALL

! Define the handlebar position
*GET, max_node_H, NODE, 0, NUM, MAXD
N, max_node_H+1, 3.960, 889.1, 0
! MPC184 elements for the handlebar
NSEL, S, NODE, , max_node_H+1
NSEL, A, NODE, , node_UP
E, max_node_H+1, node_UP


ALLSEL, ALL

/ESHAPE,1
/VIEW, 1, 1, 1, 1
EPLOT
FINISH  


! --------------------------------------------
! >>>>> SOLUTION <<<<<
! --------------------------------------------

/SOLUTION
NSUBST,10,100,2
NROPT, FULL, , OFF
NLGEOM, ON
AUTOTS, ON
ANTYPE, STATIC, NEW   !static solution

! >>>>> CONSTRAINT DEFINITION <<<<<<

! Constrain the front wheel's contact point (flip-over limit)
NSEL, S, NODE, , max_node_FC+1
 D, ALL, UX, 0
 D, ALL, UY, 0
 D, ALL, UZ, 0
 D, ALL, ROTX,0
 D, ALL, ROTY,0
 ! D, ALL, ROTZ,0 free rotation around Z axis
ALLSEL, ALL

! Constrain the rear shocks
NSEL, S, NODE, , max_node_L_SA+1, max_node_R_SA+1
 D, ALL, UY, 0
 ALLSEL, ALL


! >>>>>> LOAD DEFINITION <<<<<

! Parameters
toRad = 3.141592653589793/180.0 ! conversion factor from degrees to radians
g = 9.81 ! m/s^2
m_r = 75 ! kg rider's mass 
m = 120 ! kg scooter's mass
M = m+m_r ! kg total mass
m_batteries = 21 ! kg batteries' mass
m_engine = 30 ! kg engine's mass
a_x = 1.2*g ! m/s^2 acceleration

! Rider's weight and inertia forces
W_1 = 480 ! N rider's weigh on cradle
W_2 = 320 ! N rider's weigh on front seat
F_rider = m_r*a_x ! N rider's inertia force
F_man = 0.6*F_rider ! N rider's inertia force on handlebar
F_ped = 0.3*F_rider ! N rider's inertia force on cradle
F_sel = 0.1*F_rider ! N rider's inertia force on front seat

! Scooter's weight and inertia forces
W_batteries = m_batteries*g ! N batteries' weight
F_batteries = m_batteries*a_x ! N batteries' inertia force
W_engine = m_engine*g ! N engine's weight
F_engine = m_engine*a_x ! N engine's inertia force
W_structure = (M - m_batteries - m_engine)*g ! N structure's weight
F_structure = (M - m_batteries - m_engine)*a_x ! N structure's inertia force

! Apply rider's inertia force on handlebar
NSEL, S, NODE, , max_node_H+1
 F, max_node_H+1, FX, -F_man
ALLSEL, ALL

! Apply rider's inertia force on cradle (distributed load)
theta_cradle = 8*toRad ! Cradle beams angle with respect to x global axis 
*SET, F_ped_half, F_ped*0.5
LSEL, S, LINE,,3 ! Left side line 3 
*GET, L3_length, LINE, 3, LENGTH
*SET, f_ped_half_press, F_ped_half/L3_length ! Pressure on the line (N/mm)
*SET, f_ped_half_press_x_rot, f_ped_half_press*cos(theta_cradle) ! rotated x
*SET, f_ped_half_press_y_rot, f_ped_half_press*sin(theta_cradle) ! rotated y 
ESLL, S
SFBEAM, ALL, 3, PRES, -f_ped_half_press_x_rot
SFBEAM, ALL, 1, PRES, -f_ped_half_press_y_rot
LSEL, S, LINE,,13  ! Right side line 13
ESLL, S
SFBEAM, ALL, 3, PRES, -f_ped_half_press_x_rot
SFBEAM, ALL, 1, PRES, -f_ped_half_press_y_rot
ALLSEL, ALL

! Apply rider's weight on cradle (distributed load)
*SET, W_1_half, W_1*0.5
LSEL, S, LINE,,3 ! Left side line 3
*SET, w_1_half_press, W_1_half/L3_length
ESLL, S
SFBEAM, ALL, 1, PRES, w_1_half_press
LSEL, S, LINE,,13  ! Right side line 13
ESLL, S
SFBEAM, ALL, 1, PRES, w_1_half_press
ALLSEL, ALL

! Apply rider's weight and inertia on front seat (distributed load)
theta_cradle = 18*toRad
*SET, W_2_half, W_2*0.5
*SET, F_sel_half, F_sel*0.5
*GET, L9_length, LINE, 9, LENGTH
LSEL, S, LINE,,9 ! Left side line 9
*SET, w_2_half_press, W_2_half/L9_length ! Pressure on the line (N/mm)
*SET, f_2_half_press, F_sel_half/L9_length
*SET, f_2_half_press_x_rot, f_2_half_press*cos(theta_cradle) ! rotated x
*SET, f_2_half_press_y_rot, f_2_half_press*sin(theta_cradle) ! rotated y
ESLL, S
SFBEAM, ALL, 1, PRES, -w_2_half_press
SFBEAM, ALL, 1, PRES, f_2_half_press_y_rot
SFBEAM, ALL, 3, PRES, -f_2_half_press_x_rot
LSEL, S, LINE,,19  ! Right side line 19
ESLL, S
SFBEAM, ALL, 1, PRES, -w_2_half_press
SFBEAM, ALL, 1, PRES, f_2_half_press_y_rot
SFBEAM, ALL, 3, PRES, -f_2_half_press_x_rot

ALLSEL, ALL

! Apply batteries' weight and inertia on the cradle (distributed load)
*SET, W_batteries_half, W_batteries*0.5
*SET, F_batteries_half, F_batteries*0.5
LSEL, S, LINE,,4 ! Left side line 4
*GET, L4_length, LINE, 4, LENGTH
*SET, w_batteries_half_press, W_batteries_half/L4_length ! Pressure on the line (N/mm)
*SET, f_batteries_half_press, F_batteries_half/L4_length ! Pressure on the line (N/mm)
*SET, f_batteries_half_press_x_rot, f_batteries_half_press*cos(theta_cradle) ! rotated x
*SET, f_batteries_half_press_y_rot, f_batteries_half_press*sin(theta_cradle) ! rotated y
ESLL, S
SFBEAM, ALL, 1, PRES, w_batteries_half_press
SFBEAM, ALL, 3, PRES, -f_batteries_half_press_x_rot
SFBEAM, ALL, 1, PRES, -f_batteries_half_press_y_rot
LSEL, S, LINE,,14  ! Right side line 14
ESLL, S
SFBEAM, ALL, 1, PRES, w_batteries_half_press
SFBEAM, ALL, 3, PRES, -f_batteries_half_press_x_rot
SFBEAM, ALL, 1, PRES, -f_batteries_half_press_y_rot
ALLSEL, ALL

! Apply engine's weight and inertia near the swingarm pivot
NSEL, S, NODE, , max_node_C+1 ! Center of the swingarm pivot
F, max_node_C+1, FY, -W_engine
F, max_node_C+1, FX, -F_engine
ALLSEL, ALL

! Apply horizontal and vertical accelerations to the global reference frame to account for the rest of the scooter's mass
ACEL, a_x, g, 0


OUTRES, ALL, ALL
EQSLV, PCG
SOLVE

FINISH
/REPLOT

! --------------------------------------------
! >>>>> POSTPROCESSING <<<<<
! --------------------------------------------

/POST1
SET,1
PLDISP,2
PLESOL, S,EQV, 0,1.0