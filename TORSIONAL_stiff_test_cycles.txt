FINISH
/CLEAR, START, NEW  ! Clear previous data and start a new session

! >>>>>> INPUT PARAMETERS <<<<<

!element thin tube
R_in_thin = 15.05	!inner radius
R_out_thin = 17.45	!outer radius

!element thick tube
R_in_thick = 20.45	!inner radius
R_out_thick = 24.13	!outer radius

! >>>>> MATERIAL PROPERTIES <<<<< TO BE MODIFIED
E_Young = 210000  
ni = 0.29	    
Density=7.85e-6		

! >>>>> other parameters <<<<<
E_length_straight = 10	! element length (straight part)
E_length_arc = 1
Eps = 1E-4	! selection tolerance


! >>>>> PREPROCESSOR <<<<<
/PREP7 

ET, 1, BEAM189  
! Material properties
MP, EX, 1, E_Young  
MP, PRXY, 1, ni     
MP, DENS, 1, density  

! Data thick cross section (Cylindrical Tube)
SECTYPE, 1, BEAM, CTUBE, thicktube
SECDATA, R_in_thick, R_out_thick  

! Data thin cross section (Cylindrical Tube)
SECTYPE, 2, BEAM, CTUBE, thintube
SECDATA, R_in_thin, R_out_thin  ! Parametri della sezione del tirante (tubo cavo)

! >>>>> GEOMETRY DEFINITION <<<<<

! Keypoints and lines creation (symmetric part, left side)
K,1,-20.98,113.55,0
K,2,-6.22,33.68,140.67
K,3,62.97,44.17,139.36 ! Centro arco
K,4,67.84,9.53,200
K,5,565.45,79.47,200
K,6,555.71,148.79,200 ! Centro arco
K,7,611.61,106.66,200
K,8,821.60,385.33,200
K,9,877.51,343.2,200 ! Centro arco
K,10,855.88,409.78,200 
K,11,1226.2,530.11,200

! Keypoints and lines creation (symmetric part, right side)
K,12,-6.22,33.68,-140.67  
K,13,62.97,44.17,-139.36 ! Arc center
K,14,67.84,9.53,-200
K,15,565.45,79.47,-200 
K,16,555.71,148.79,-200 ! Arc center
K,17,611.61,106.66,-200
K,18,821.60,385.33,-200 
K,19,877.51,343.2,-200 ! Arc center
K,20,855.88,409.78,-200 
K,21,1226.2,530.11,-200

!Transverse sections
K,22,1048.21,395.72,0 ! Arc center
K,23,1105.73,490.96,200
K,24,1105.73,490.96,-200
K,25,672.18,187.04,200
K,26,672.18,187.04,-200
K,27,300,42.16,200
K,28,300,495.44,0 ! Arc center
K,29,300,42.16,-200

!central section (steering wheel pivot)
K,30,300,0,0
K,31,58.25,0,0
K,32,58.25,70,0 ! Arc center
K,33,-10.58,57.28,0
K,34,-113.26,612.95,0
K,35,-54.65,751.03,0
K,36,-171.87,474.88,0

! Lines creation

! symmetric part, left side
L,1,2
LARC,2,4,3,70
L,4,27
L,27,5
LARC,5,7,6,70
L,7,25
L,25,8
LARC,8,10,9,70
L,10,23
L,23,11

! symmetric part, right side
L,1,12
LARC,12,14,13,70
L,14,29
L,29,15
LARC,15,17,16,70
L,17,26
L,26,18
LARC,18,20,19,70
L,20,24
L,24,21

! Transverse sections
LARC,11,21,22,300
L,23,24
L,25,26
LARC,27,30,28,500
LARC,30,29,28,500

! Central section (steering wheel pivot)
L,30,31
LARC,31,33,32,70
L,33,1
L,1,34
L,35,34
L,34,36

! Element definition

! Left side straight lines
LSEL,S,LINE,,1
LSEL,A,LINE,,3
LSEL,A,LINE,,4
LSEL,A,LINE,,6
LSEL,A,LINE,,7
LSEL,A,LINE,,9
LSEL,A,LINE,,10
! Right side straight lines
LSEL,A,LINE,,11
LSEL,A,LINE,,13
LSEL,A,LINE,,14
LSEL,A,LINE,,16
LSEL,A,LINE,,17
LSEL,A,LINE,,19
LSEL,A,LINE,,20
! Transverse straight lines
LSEL,A,LINE,,22
LSEL,A,LINE,,23


LESIZE, ALL,E_length_straight
LATT,1,,1,,,,2
LSEL,ALL,ALL

! Left side arc lines
LSEL,S,LINE,,2
LSEL,A,LINE,,5
LSEL,A,LINE,,8
! Right side arc lines
LSEL,A,LINE,,12
LSEL,A,LINE,,15
LSEL,A,LINE,,18
! Transverse arc lines
LSEL,A,LINE,,21
LSEL,A,LINE,,24
LSEL,A,LINE,,25

LESIZE, ALL,E_length_arc
LATT,1,,1,,,,2
LSEL,ALL,ALL
! Central section straight lines (thick tube)
LSEL,S,LINE,,26
LSEL,A,LINE,,28
LSEL,A,LINE,,29
LSEL,A,LINE,,30
LSEL,A,LINE,,31
LESIZE, ALL,E_length_straight
LATT,1,,1,,,,1
LSEL,ALL,ALL
! Central section arc lines (thick tube)
LSEL,S,LINE,,27
LESIZE, ALL,E_length_arc
LATT,1,,1,,,,1
LSEL,ALL,ALL

! ORIENTATION OF SECTION FOR L3, L4
LSEL, S, LINE,,3
LSEL, A, LINE,,4
LATT,1,,1,,6,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L13, L14
LSEL, S, LINE,,13
LSEL, A, LINE,,14
LATT,1,,1,,16,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L9, L10
LSEL, S, LINE,,9
LSEL, A, LINE,,10
LATT,1,,1,,9,,2
LSEL, ALL, ALL
! ORIENTATION OF SECTION FOR L19, L20
LSEL, S, LINE,,19
LSEL, A, LINE,,20
LATT,1,,1,,19,,2
LSEL, ALL, ALL

LMESH,ALL
NUMMRG,NODE,0.01

! Define the center of the swingarm pivot LEFT SIDE (Master nodes)
*GET, max_node_L, NODE, 0, NUM, MAXD
N, max_node_L+1, 664.58, 86.38, 205.0

ET,2,184
KEYOPT,2,1,1
TYPE,2
! Select nodes for the left side (Slave nodes)
LSEL, S, LINE,,5
LSEL, A, LINE,,6
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_L+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO

ALLSEL, ALL

! Define the center of the swingarm pivot RIGHT SIDE (Master nodes)
*GET, max_node_R, NODE, 0, NUM, MAXD
N, max_node_R+1, 664.58, 86.38, -205.0
! Select nodes for the right side (Slave nodes)
LSEL, S, LINE,,15
LSEL, A, LINE,,16
NSLL, S,1
! Create MPC184 elements
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
    *GET, n_node, NODE, 0, NUM, MAX
    E,max_node_R+1,n_node
    NSEL, U, NODE, , n_node
*ENDDO

! Local RF in LEFT SIDE center
LOCAL,11,0         
CSYS,0
LOCAL,11,0, 664.58, 86.38, 205.0, 67, 0, 0
NSEL, S, NODE, , max_node_L+1
CSYS,11
NROTAT, ALL
CSYS,0
ALLSEL, ALL

! Local RF in RIGHT SIDE center
LOCAL,12,0
CSYS,0
LOCAL,12,0, 664.58, 86.38,-205.0, 67, 0, 0
NSEL, S, NODE, , max_node_R+1
CSYS,12
NROTAT, ALL
CSYS,0
ALLSEL, ALL

/ESHAPE,1
/VIEW, 1, 1, 1, 1
EPLOT
FINISH  

! >>>>> SOLUTION <<<<<
/SOLUTION
ANTYPE, STATIC, NEW   !static solution

! Steering head constraint
LSEL, S, LINE,,30
LSEL, A, LINE,,31
NSLL, S,1

 D, ALL, UX,0
 D, ALL, UY,0
 D, ALL, UZ,0
 D, ALL, ROTX,0
 D, ALL, ROTY,0
 D, ALL, ROTZ,0

toRad = 3.14159265359/180 ! convert degrees to radians
toDeg = 180/3.14159265359 ! convert radians to degrees
ang = 67*toRad

! TORSIONAL STIFFNESS CHARACTERIZATION
! vectors
*DIM, forces, ARRAY, 5
*DIM, angles, ARRAY, 5
*DIM, Kt, ARRAY, 5

forces(1) = 1000 ! N
forces(2) = 2000 ! N
forces(3) = 3000 ! N
forces(4) = 4000 ! N
forces(5) = 5000 ! N

toRad = 3.14159265359/180
toDeg = 180/3.14159265359
ang = 67*toRad
intx_mm = 400       ! mm
intx = intx_mm/1000 ! m

*DO, i, 1, 5
  /SOLUTION ! >>>>> SOLUTION <<<<<
  F = forces(i)   ! N
  Torque = F*intx ! Nm
  Fx = F*COS(ang)
  Fy = F*SIN(ang)
  ! apply torque
  NSEL, S, NODE, , max_node_L+1
  F, max_node_L+1, FX, Fx
  NSEL, S, NODE, , max_node_R+1
  F, max_node_R+1, FX, -Fx
  ALLSEL,ALL

  outres,all,all
  Eqslv,pcg
  SOLVE

  ! >>>>>> POSTPROCESSING <<<<<
  /POST1
  SET, 1
  ! get displacement values (global reference system)
  *GET, ux_L, NODE, max_node_L+1, U, X
  *GET, uy_L, NODE, max_node_L+1, U, Y
  *GET, uz_L, NODE, max_node_L+1, U, Z
  utot_L = SQRT(ux_L**2 + uy_L**2 + uz_L**2)

  *GET, ux_R, NODE, max_node_R+1, U, X
  *GET, uy_R, NODE, max_node_R+1, U, Y
  *GET, uz_R, NODE, max_node_R+1, U, Z
  utot_R = SQRT(ux_R**2 + uy_R**2 + uz_R**2)
  ! get to local ref frame (CSYS 11 (L) and 12 (R))
  ROTZ = 67 ! deg
  COSR = COS(ROTZ*toRad)
  SINR = SIN(ROTZ*toRad)
  ux_L_local = ux_L*COSR + uy_L*SINR
  uy_L_local = -ux_L*SINR + uy_L*COSR
  uz_L_local = uz_L
  ux_R_local = ux_R*COSR + uy_R*SINR
  uy_R_local = -ux_R*SINR + uy_R*COSR
  uz_R_local = uz_R
  ! get angle around the rotation axis
  ang_L = ATAN2(ux_L_local, intx_mm*0.5 - uz_L_local)*toDeg ! deg
  ang_R = ATAN2(ux_R_local, intx_mm*0.5 - uz_R_local)*toDeg ! deg

  angles(i) = ang_L ! deg
  Kt(i) = Torque/ang_L ! Nm/deg
  /REPLOT
  PLDISP,2

*ENDDO

*CFOPEN, output_vectors, txt
*DO, ii, 1, 5
    *VWRITE, Kt(ii)
    (F10.4)
*ENDDO
*CFCLOS